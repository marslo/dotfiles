" =============================================================================
"      FileName : autocmd
"        Author : marslo.jiao@gmail.com
"       Created : 2010-10
"       Version : 2.0.2
"    LastChange : 2026-02-05 20:43:19
" =============================================================================

" vint: -ProhibitAutocmdWithNoGroup

" /**************************************************************
"              _                           _
"   __ _ _   _| |_ ___   ___ _ __ ___   __| |
"  / _` | | | | __/ _ \ / __| '_ ` _ \ / _` |
" | (_| | |_| | || (_) | (__| | | | | | (_| |
"  \__,_|\__,_|\__\___/ \___|_| |_| |_|\__,_|
"
" **************************************************************/
if has( 'autocmd' )

  " assumes set ignorecase smartcase
  augroup DynamicSmartcase
      autocmd!
      autocmd CmdLineEnter : set nosmartcase
      autocmd CmdLineLeave : set smartcase
  augroup END

  augroup AutoDocToc
    autocmd!
    autocmd BufWritePost *.md
            \ let fname = expand('%:p') |
            \ if fname !~# '\/\v(README|SUMMARY)\.md$' |
            \   echom 'DocToc Update: '.expand('%') |
            \   silent! execute 'DocTocUpdate' |
            \   checktime |                                                   " force reload file to prevent async modification conflict"
            \ endif
    " if index( ['README', 'SUMMARY'], expand('%:r') ) == -1
    "   autocmd BufWritePost  *\(.md\)  silent :DocTocUpdate                  " automatic build doctoc when save it
    " endif
  augroup END

  augroup DevOps
    autocmd!
    autocmd Filetype           Dockerfile          set filetype=dockerfile  syntax=dockerfile
    autocmd FileType           json,markdown,yaml  set tabstop=2 softtabstop=2 shiftwidth=2 | setlocal foldmethod=indent
    autocmd Syntax             yaml                normal zM
    autocmd BufEnter           *\(.md\|.markdown\) exe 'noremap <F5> :!"/Applications/Google Chrome.app/Contents/MacOS/Google Chrome" %:p "--no-gpu"<CR>'
    autocmd BufRead,BufNewFile .*ignore            set filetype=ignore
    autocmd FileType           ignore,gitconfig    setlocal commentstring=#\ %s
    autocmd FileType           json                setlocal commentstring=//\ %s
    " for json conccellevel issue
    autocmd BufEnter           *.json              ++nested call timer_start(10, { -> execute('setlocal concealcursor=') })
    " force set jsonc for json with comments: $ npm i -g vscode-langservers-extracted
    autocmd BufNewFile,BufRead *tsconfig*.json,
          \*eslintrc*.json,
          \*babelrc*.json,
          \*.code-workspace,
          \.vscode/*.json,
          \*[Ss]ettings*.json,*.json.current,*keybindings.json
          \ set filetype=jsonc
    " autocmd BufRead,BufNewFile *.json                                    setlocal filetype=jsonc
    autocmd FileType           markdown,html
            \ let g:AutoPairsCompleteOnlyOnSpace = 1 |
            \ let b:AutoPairs = autopairs#AutoPairsDefine({
            \   '<div>':'</div>', '<font>':'</font>', '<a>':'</a>', '<p>':'</p>',
            \   '<table>':'</table>', '<tbody>':'</tbody>',
            \   '<thread>':'</thread>', '<th>':'</th>', '<td>':'</td>',
            \   '<kbd>':'</kbd>',
            \   '<u>':'</u>'
            \ })
    if index( ['project.config', 'groups'], expand('%:r') ) == -1
      autocmd BufRead,BufNewFile *.config          set syntax=gitconfig noexpandtab
    endif
    autocmd BufNewFile,BufRead                                                " git config files
            \ */.git/config,
            \ .gitconfig,
            \ .gitmodules,
            \ gitconfig,
            \ ~/.marslo/.gitalias
            \ setfiletype gitconfig
  augroup END

  augroup ShellScript
    autocmd!
    autocmd Syntax       bash                set filetype=sh
    autocmd FileType     sh,bash,shell
            \ set tabstop=2 softtabstop=2 shiftwidth=2 expandtab |
            \ silent! keepjumps retab!                                        " automatic retab when open it
    autocmd BufWritePre  *.sh                                                 " automatic retab when save
            \ if &modifiable && !&readonly | silent! keepjumps retab! | endif
    autocmd BufWritePost *.sh                                                 " automatic redraw for shellcheck
            \ if has('syntax') && exists('g:loaded_shellcheck') | silent! redraw! | endif
    " autocmd BufWritePost *.sh  silent !chmod +x %
  augroup END

  augroup Groovy
    autocmd Filetype   Groovy                set filetype=groovy syntax=groovy
    autocmd Syntax     groovy,Jenkinsfile    normal zM
    autocmd FileType   groovy,Jenkinsfile    setlocal foldmethod=indent
            \| setlocal tabstop=2 softtabstop=2 shiftwidth=2
    autocmd FileType   Jenkinsfile           setlocal commentstring=//\ %s

    " https://vim.fandom.com/wiki/Syntax_folding_for_Java
    autocmd FileType   java                  setlocal foldmarker=/**,**/ foldmethod=marker foldcolumn=1
    if did_filetype()                      | finish      | endif
    if getline(1) =~# '^#!.*[/\\]groovy\>' | setf groovy | endif              " to setup filetype to groovy if first line matches `#!.*[/\\]groovy`
  augroup END

  augroup Python
    autocmd BufNewFile,BufRead python
            \| setlocal noexpandtab
            \| silent retab!
            \| setlocal expandtab tabstop=4 softtabstop=4 shiftwidth=4 autoindent
            \| setlocal fileformat=unix
    autocmd FileType python syntax keyword pythonDecorator print self
    autocmd FileType python set isk-=.
    autocmd Syntax   python setlocal foldmethod=indent
    autocmd Syntax   python normal zM
    autocmd FileType python map  <buffer> <F9> :w<CR>:exec '!python3' shellescape(@%, 1)<CR>
    autocmd FileType python imap <buffer> <F9> <esc>:w<CR>:exec '!python3' shellescape(@%, 1)<CR>
  augroup END

  augroup Vimrc
    if index( ['vimrc.d'], split(expand('%:p:h'), '/')[-1] ) >= 0
      autocmd! BufWritePost ~/.marslo/vimrc.d/* silent! source $MYVIMRC
               \| echohl WarningMsg
               \| echom expand('%:p') . " changed! " . $MYVIMRC . " sourced!"
               \| echohl None
               \| execute 'silent redraw!'
    endif

    if IsNvim() && '/'.join(split(expand('%:p'), '/')[0:3], '/') == stdpath('config')
      autocmd! BufWritePost ~/.config/nvim/*    silent! source $MYVIMRC
               \| echohl WarningMsg
               \| echom expand('%:p') . " changed! " . $MYVIMRC . " sourced!"
               \| echohl None
               \| execute 'silent redraw!'
    endif
  augroup END

  augroup CustomTabularize
    autocmd!
    " https://github.com/jwhitley/vimrc/blob/master/.vimrc
    " autocmd VimEnter * if exists(":Tabularize") | exe ":AddTabularPattern! bundles /[^ ]\\+\\//l1r0" | endif
    " add for plugin/TabularMaps.vim
    autocmd VimEnter * if exists(":Tabularize") | exe ":AddTabularPattern 1,  /^[^,]*\zs,/r1c1l0" | endif
    autocmd VimEnter * if exists(":Tabularize") | exe ":AddTabularPattern 1=  /^[^=]*\zs="        | endif
    autocmd VimEnter * if exists(":Tabularize") | exe ":AddTabularPattern 1== /^[^=]*\zs=/r1c1l0" | endif
  augroup END

  augroup ColumnLimit
    set colorcolumn=80
    highlight ColumnLimit    guibg=NONE    guifg=#4e4e4e    ctermfg=240
    autocmd!
    let columnLimit = 80
    let pattern      = '\%<' . ( columnLimit+1 ) . 'v.\%>' . columnLimit . 'v'
    let w:m1         = matchadd( 'ColumnLimit', pattern, -1 )
  augroup END

  augroup TerminalFormat
    if IsNvim()
      " autocmd! TermOpen,TermEnter * :IndentLinesDisable
      " autocmd! BufWinEnter        * if &l:buftype ==# 'help' | :IndentLinesDisable | endif
      autocmd! TermOpen             * setlocal nonumber norelativenumber modifiable nospell
      autocmd  TermClose            * echom 'Terminal exited with status '..v:event.status
    endif
  augroup END

  augroup AutoSavePos
    if v:version > 703
      " autocmd FocusLost   * set norelativenumber
      " autocmd FocusGained * set relativenumber                              " impacted NerdTree and terminal mode
      autocmd InsertEnter * set norelativenumber
      autocmd InsertLeave * set relativenumber
      autocmd CmdwinEnter * set norelativenumber
      autocmd CmdwinLeave * set relativenumber
      autocmd CmdwinEnter * let b:ei_save = &eventignore | set eventignore=CursorHold,InsertEnter
      autocmd CmdwinLeave * let &eventignore = b:ei_save
    endif
  augroup END

  augroup Defaults
    " foldmethod to indent for all filetypes except help and vim-plug
    autocmd BufWinEnter *
            \  if empty(&filetype) || (index(['help', 'vim-plug'], &filetype) == -1)
            \|   if &l:foldmethod ==# 'manual' | setlocal foldmethod=indent | endif
            \| endif
    " for modeline
    autocmd BufReadPost *
            \  if getline(1) =~# 'vim:.*ft=help' && &filetype !=# 'help'
            \|   doautocmd FileType
            \| endif
    " autocmd BufRead,BufNewFile,BufWinEnter *                 if !did_filetype() && &ft ==# '' | setfiletype Jenkinsfile | endif
    autocmd BufRead,BufNewFile,BufWinEnter *                   if !did_filetype() && &ft ==# '' | set syntax=Jenkinsfile | endif
    autocmd BufWinEnter        *                               silent! loadview                                     " autocmd BufWinLeave * silent! mkview
    autocmd Syntax             *                               syn match ExtraWhitespace /\s\+$\| \+\ze\t/
    autocmd BufEnter           *                               if &diff | let g:blamer_enabled=0 | endif            " ╮ disable git blame in diff mode
    " autocmd BufEnter         *                               if ! empty(&key) | let g:blamer_enabled=0 | endif    " ╯ and encrypt mode
    autocmd BufWritePre        *                               %s/\s\+$//e | %s/\r$//e   " automatic remove trailing space
    autocmd BufWritePre        *\(.ltsv\|.config\|.diffs\)\@<! silent! retab!            " automatic retab: https://vim.fandom.com/wiki/Remove_unwanted_spaces
    autocmd BufRead,BufNewFile *.ltsv                          set filetype=ltsv syntax=groovy noexpandtab
    autocmd BufRead,BufNewFile *.t                             set filetype=perl
    autocmd QuickFixCmdPost    *grep*                          cwindow
    autocmd FileType           make,snippet,robot              setlocal tabstop=4 softtabstop=4 shiftwidth=4
    autocmd FileType           snippets
            \ setlocal expandtab tabstop=2 softtabstop=2 shiftwidth=2 |
            \ setlocal commentstring=#\ %s
    autocmd FileType           help                            setlocal nofoldenable
    autocmd Syntax             c,cpp,xml,html,xhtml            normal zM
    " autocmd VimLeave         *                               silent :%bd                                          " :windo bd
  augroup END

  let s:ft_settings = {
        \ 'help': {'foldenable': v:false},
        \ 'vim-plug': {'foldenable': v:false, 'relativenumber': v:false, 'number': v:false },
        \ 'markdown': {'spell': 1}
        \ }

  function! s:ApplyFTSettings() abort
    if has_key(s:ft_settings, &filetype)
      for [opt, val] in items(s:ft_settings[&filetype])
        let current_val = eval('&l:' . opt)
        " let current_val = getbufvar('%', '&' . opt)
        if type(val) == v:t_bool
          " if `val = v:false`, the option will be `no{opt}`
          execute 'setlocal' (val ? '' : 'no') . opt
        else
          if current_val != val | execute 'setlocal ' . opt . '=' . val | endif
        endif
      endfor
    else
      " if no foldmethod is set, use indent as default
      if &l:foldmethod ==# 'manual' | setlocal foldmethod=indent | endif
    endif
  endfunction

  augroup LAST_OVERRIDE
    autocmd!
    autocmd FileType    *        ++nested call s:ApplyFTSettings()
    autocmd BufWinEnter *        call s:ApplyFTSettings()
    " autocmd BufWinEnter __Plug__ setlocal nofoldenable nonumber relativenumber=0
    " disable for copilot-chat
    autocmd BufEnter    *        if &filetype ==# 'copilot-chat'
            \| setlocal nonumber norelativenumber modifiable nospell
            \| endif

    " resize the copilot log window
    autocmd! BufWinEnter copilot:///log call timer_start(50, {-> execute('resize -5')})
    autocmd   BufEnter    *        if bufname('%') ==# 'copilot:///log'
             \| setlocal nonumber norelativenumber modifiable nospell wrap
             \| endif
    " disable for copilot panel
    autocmd  BufEnter    *        if bufname('%') ==# 'copilot:///panel'
             \| setlocal nonumber norelativenumber modifiable nospell
             \| endif
  augroup END

  " to fix the <SNR>13_StarSetf issue -- no-augroup
  autocmd! BufReadPost */.gitconfig.d/*
  autocmd! BufNewFile  */.gitconfig.d/*
  autocmd! BufReadPost,BufNewFile */.gitconfig.d/* setfiletype gitconfig

endif

" vim:tabstop=2:softtabstop=2:shiftwidth=2:expandtab:filetype=vim:
